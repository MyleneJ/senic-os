#@TYPE: Machine
#@NAME: senic-hub
#@DESCRIPTION: Machine configuration for Senic Hub that is based on NanoPi NEO v1.3 + Baseboard

include conf/machine/include/soc-family.inc
require conf/machine/include/tune-cortexa7.inc

SOC_FAMILY = "sun8i"
DEFAULTTUNE = "cortexa7hf-neon-vfpv4"

# Kernel
PREFERRED_PROVIDER_virtual/kernel ?= "linux-senic"
PREFERRED_VERSION_linux = "4.10+git%"
KERNEL_IMAGETYPE = "zImage"
KERNEL_DEVICETREE = "sun8i-h3-senic-hub.dtb"
SERIAL_CONSOLE = "115200 ttyS0"

# Install kernel and devicetree in /boot for Mender.io
RDEPENDS_kernel-base += "kernel-image kernel-devicetree"

# Bootloader
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-senic"
PREFERRED_PROVIDER_u-boot = "u-boot-senic"
PREFERRED_VERSION_u-boot = "v2017.03+git%"
UBOOT_ARCH = "arm"
UBOOT_MACHINE = "senic_hub_defconfig"

PREFERRED_PROVIDER_u-boot-fw-utils = "u-boot-fw-utils-senic"

# To build the u-boot image
EXTRA_IMAGEDEPENDS += "virtual/bootloader"

# Add Bluetooth as machine features as USB dongles will be used
MACHINE_FEATURES += "bluetooth wifi"

# the rtc feature is incompatible with our packagroup constellation, so we must disable it
# (also our board doesn't have a realtime clock anyway...)
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"

# To create a wic image to deploy it on a SD-card
IMAGE_FSTYPES = "ext4"
EXTRA_IMAGEDEPENDS += "dosfstools-native mtools-native"
MACHINE_EXTRA_RDEPENDS += "kernel-modules"
